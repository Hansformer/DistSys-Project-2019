# To show the debug dialog, comment out
# INITIALIZER:
# and uncomment the following line
# if __name__=="__main__":
INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI

    root = Tk()
    root.withdraw()
    topLevel = Toplevel(root)
    topLevel.resizable(width="NO", height="NO")
    topLevel.title("DWatch")
    g = DWatchGUI(topLevel, eventhandler)

    eventhandler.start()

    root.mainloop()
    exit()


# [DS] = default state
STATECHART:
    Timer [DS] [CS]
        TimerTick [DS] [CS]
        ChronoTick [DS] [CS]
            ChronoTickOn
            ChronoTickOff [DS]

    Chrono [DS] [CS]
        ChronoOn
        ChronoOff [DS]
        ChronoRunningInForeground
        ChronoRunningInBackground
    
    Light [DS] [CS]
        LightOff [DS]
        LightOn
        LightTimeout

# S = the source state of the transition.
# N = the destination state of the transition.
# E = the event that triggers the transition.
# O = a list of commands to be executed as the output of the transition. [DUMP(...)] is a predefined macro that prints a message.

TRANSITION:
    S: Timer.TimerTick
    N: Timer.TimerTick
    T: 1
    O: print "Time Tick"
       g.increaseTimeByOne()

# TODO: Change time to 0.01 for final version
TRANSITION:
    S: Timer.ChronoTick.ChronoTickOff
    N: Timer.ChronoTick.ChronoTickOn
    T: 0.25 [OTT]
    C: [INSTATE("Chrono.ChronoRunningInForeground")] or [INSTATE("Chrono.ChronoRunningInBackground")]

# TODO: Change time to 0.01 for final version
TRANSITION:
    S: Timer.ChronoTick.ChronoTickOn
    N: Timer.ChronoTick.ChronoTickOn
    C: [INSTATE("Chrono.ChronoRunningInForeground")] or [INSTATE("Chrono.ChronoRunningInBackground")]
    T: 0.25
    O: print "Chrono Tick"
       g.increaseChronoByOne()

TRANSITION:
    S: Chrono.ChronoOff
    N: Chrono.ChronoOn
    E: changeMode
    O: print "Switched to Chrono Mode"
       g.resetChrono()
       g.refreshChronoDisplay() 

TRANSITION:
    S: Chrono.ChronoRunningInBackground
    N: Chrono.ChronoRunningInForeground
    E: changeMode
    O: print "Switched to Chrono Mode"

TRANSITION:
    S: Chrono.ChronoOn
    N: Chrono.ChronoOn
    T: 0.05
    O:
       g.refreshChronoDisplay() 

TRANSITION:
    S: Chrono.ChronoOn
    N: Chrono.ChronoOff
    E: changeMode
    O: print "Switched to Timer Mode"
       g.refreshDateDisplay()

TRANSITION:
    S: Chrono.ChronoOn
    N: Chrono.ChronoRunningInForeground
    E: initChrono
    O: print "Starting Chrono"

TRANSITION:
    S: Chrono.ChronoRunningInForeground
    N: Chrono.ChronoRunningInForeground
    T: 0.05
    O:
       g.refreshChronoDisplay() 

TRANSITION:
    S: Chrono.ChronoRunningInForeground
    N: Chrono.ChronoRunningInBackground
    E: changeMode
    O: print "Switched to Timer Mode"
       g.refreshDateDisplay()


TRANSITION:
    S: Light.LightOff
    N: Light.LightOn
    E: lightOn
    O: print "set light on"
       g.setIndiglo()

TRANSITION:
    S: Light.LightOn
    N: Light.LightTimeout
    E: lightOff
    O: print "Timeout for turning light off"

TRANSITION:
    S: Light.LightTimeout
    N: Light.LightOff
    T: 2 [OTT]
    O: g.unsetIndiglo()
       print "Light turned off"
